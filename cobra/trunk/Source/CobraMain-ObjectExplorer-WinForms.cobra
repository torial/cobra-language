"""
Cobra Compiler WinForms Explorer

This runs the Cobra compiler as usual, but then follows it up with a GUI window (using WinForms) that
enables you to explore all the objects that resulted from compilation including the compiler, modules,
namespaces, declarations, statements and expressions.

"""

use System.Windows.Forms


class CobraMain

	shared

		var _willTimeIt as bool

		pro willTimeIt from var
		
		def main
			has STAThread
			# CobraCore.willCheckAll = false
			
			# Run the command line as usual:
			startTime = DateTime.now
			exc as Exception?
			try
				try
					commandLine = CommandLine()
					commandLine.run
				finally
					if _willTimeIt
						print 'timeit = [DateTime.now.subtract(startTime)]'
			catch exc as Exception?
				pass

			# Now explore the results:
			Application.enableVisualStyles
			form = ObjectExplorer()
			form.text = 'Cobra Compiler WinForms Explorer'
			if exc, form.addKeyValue('Exception', exc)
			form.addKeyValue('CommandLine', commandLine)
			form.addKeyValue('Compiler', commandLine.compiler)
			form.addKeyValue('Messages', commandLine.compiler.messages)

			topNameSpaces = List<of NameSpace>()
			for module in commandLine.compiler.modules
				if module inherits CobraModule
					if not module.isCobraLibrary
						topNameSpaces.add(module.topNameSpace)
			form.addKeyValue('Namespaces', topNameSpaces)

			form.show
			Application.run(form)
