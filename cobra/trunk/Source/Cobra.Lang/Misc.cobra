namespace Cobra.Lang

	class EmptyStream<of T>
		implements IEnumerable<of T>
	
		def getEnumerator as IEnumerator<of T>
			yield break
	
		def getEnumerator as System.Collections.IEnumerator
			implements System.Collections.IEnumerable
			return .getEnumerator 
	
	interface HasAppendKeyValue

		def appendKeyValue(key as String, value)
			"""
			Append a specific key+value pair to the receiver.
			Typically these are displayed in a grid or table, in the order they were appended.
			See also: HasAppendNonPropertyKeyValues
			"""

	interface HasAppendNonPropertyKeyValues

		def appendNonPropertyKeyValues(target as HasAppendKeyValue)
			"""
			Appends key+value pairs to the target. The pairs further describe the receiver and
			are not already covered by properties of the receiver.
			For example implementations, see AssertException and CobraFrame.
			See also: HasAppendKeyValue
			"""
		
	interface ITreeBuilder
		inherits HasAppendKeyValue
		def indent
		def outdent

	class DetailedStackTraceAttribute inherits Attribute
		has AttributeUsage(AttributeTargets(Class, Struct), allowMultiple=false)
		cue init(flag as bool)
			base.init
			_flag = flag
		get flag from var as bool

	class MustUseResultAttribute inherits Attribute
		has AttributeUsage(AttributeTargets.Method, allowMultiple=false)
		pass

	class CobraDirectString is extern
		"""
		Used internally for assert, require and ensure to encode strings that should not be passed to CobraCore.toTechString.
		"""
		cue init(s as String)
		get string as String
			return ''
		
	class Html
		"""
		Represents HTML that doesn't need to be encoded.
		Html.toString returns the contents as-is.
		"""
		cue init(contents as String)
			base.init
			_contents = contents
		get contents from var as String
		def toString as String is override
			return _contents

	interface IHasSourceSite
		get sourceSite as SourceSite


	class RightValues<of T1, T2, T3, T4, T5, T6, T7, T8, T9>
		"""
		Utility class for MultiTargetAssignStatement.
		Initializers are not chained with this class in order to avoid method call overhead.
		"""

		cue init(value1 as T1)
			base.init
			_value1 = value1
	
		cue init(value1 as T1, value2 as T2)
			base.init
			_value1 = value1
			_value2 = value2
	
		cue init(value1 as T1, value2 as T2, value3 as T3)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4, value5 as T5)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
			_value5 = value5
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4, value5 as T5, value6 as T6)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
			_value5 = value5
			_value6 = value6
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4, value5 as T5, value6 as T6, value7 as T7)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
			_value5 = value5
			_value6 = value6
			_value7 = value7
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4, value5 as T5, value6 as T6, value7 as T7, value8 as T8)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
			_value5 = value5
			_value6 = value6
			_value7 = value7
			_value8 = value8
	
		cue init(value1 as T1, value2 as T2, value3 as T3, value4 as T4, value5 as T5, value6 as T6, value7 as T7, value8 as T8, value9 as T9)
			base.init
			_value1 = value1
			_value2 = value2
			_value3 = value3
			_value4 = value4
			_value5 = value5
			_value6 = value6
			_value7 = value7
			_value8 = value8
			_value9 = value9
	
		# to-do: change to public vars?
		get value1 from var as T1
		get value2 from var as T2
		get value3 from var as T3
		get value4 from var as T4
		get value5 from var as T5
		get value6 from var as T6
		get value7 from var as T7
		get value8 from var as T8
		get value9 from var as T9


	## For testing
	
	enum FB
		Foo
		Bar

	
	class TestDefaultToString
	
		def toString as String is override
			# return something different than the class name to tease out the different behavior that toTechString exhibits in this case (which is to add the class name to the .toString output)
			return 'Test Default To String'


	class NestedToString

		def toPrintString as String
			sb = StringBuilder(r'[')
			sep = ''
			for row in [[1, 2, 3], [1, 2, 3]]
				sb.append('[sep][row]')
				sep = ', '
			sb.append(']')
			return sb.toString


	class CustomToString

		def toPrintString as String
			return '-- to print string --'
	
		def toTechString as String
			return '-- to tech string --'


	class EnumerableToString
		implements System.Collections.IEnumerable
		
		def getEnumerator as System.Collections.IEnumerator
			return [1, 2, 3].getEnumerator


	class BadToString
	
		def toString as String is override
			throw InvalidOperationException('intentional exception in .toString')


	class BadToTechString

		def toTechString as String
			throw InvalidOperationException('intentional exception in .toTechString')

	class TestAid

		get protectedTwo as int is protected
			return 2

		get publicThree as int
			return 3

		def testGenMethod1<of T>(t as T)
			pass

		def testGenMethod2<of T>(t as T) as T
			return t

		shared
		
			def testGenMethod3<of T>(t as T)
				pass

			def testGenMethod4<of T>(t as T) as T
				return t
