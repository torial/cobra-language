class ClrNativeType
	inherits NativeType

	var _type as System.Type
	
	def init(type as System.Type)
		_type = type

	def equals(other as Object) as bool is override
		if other is this
			return true
		if other inherits ClrNativeType
			return .clrType == other.clrType
		else
			return false

	def getHashCode as int is override
		return .clrType.getHashCode

	get clrType as Type
		return _type

	get name as String is override
		return _type.name

	get fullName as String is override
		return _type.fullName to !

	get isValueType as bool is override
		return _type.isValueType

	get baseType as NativeType? is override
		return if(_type.baseType, ClrNativeType(_type.baseType), nil)


class Class
	is partial

	get isSystemObjectClass as bool is override
		return _nativeType and _nativeType.fullName == 'System.Object'

	get isSystemTypeClass as bool is override
		return _nativeType and _nativeType.fullName == 'System.Type'
