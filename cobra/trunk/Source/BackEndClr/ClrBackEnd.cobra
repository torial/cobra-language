use System.Reflection


class ClrBackEnd inherits BackEnd

	cue init(compiler as Compiler)
		base.init(compiler)

	def makePhases(phases as IList<of Phase>) is override
		phases.add(GenerateSharpCodePhase(.compiler))
		phases.add(CompileSharpCodePhase(.compiler))

	def computeOutName as String is override
		return .compiler.computeOutNameSharp
		

class GenerateSharpCodePhase inherits Phase

	cue init(c as Compiler)
		base.init(c)

	get description as String is override
		return 'Generating C# code'

	def innerRun is override
		c = .compiler
		v = c.verbosity
		
		nativeCompiler = c.options.getDefault('native-compiler', '') to String
		if nativeCompiler == 'auto'
			if CobraCore.isRunningOnMono
				cobraSharp = Path.combine(Path.getDirectoryName(Assembly.getEntryAssembly.location), 'Cobra.Sharp.dll')
				if File.exists(cobraSharp)
					if v >= 3, print '-native-compiler:auto ... Found [cobraSharp]'
					nativeCompiler = cobraSharp
				else
					nativeCompiler = ''
			else
				if v >= 3, print '-native-compiler:auto ... Not running on Mono'
				nativeCompiler = 'provider'
		c.nativeCompiler = nativeCompiler
		c.willWriteSharpToDisk = not nativeCompiler.endsWith('.dll')  # only Cobra.Sharp.dll will take in-memory source code

		c.writeSharp  # extension method
		if c.willWriteTestInvocation
			c.writeSharpTestInvocation


class CompileSharpCodePhase inherits Phase

	cue init(c as Compiler)
		base.init(c)

	get description as String is override
		return 'Compiling C# code'

	def innerRun is override
		.compiler.compileSharp
