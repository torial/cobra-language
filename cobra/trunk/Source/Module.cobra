use System.Reflection

class Module
	is abstract, partial
	inherits Node

	# TODO: Seems that much of Module's guts should be moved down to CobraModule

	var _fileName as String
	var _docString as String?

	def init(fileName as String, verbosity as int, docString as String)
		require fileName.length
		base.init
		_fileName = fileName
		_docString = docString
		# TODO: what's going on with verbosity?

	get docString from var

	get fileName from var

	get isCobraLibrary as bool
		return _fileName.endsWith('CobraLang.cobra') or _fileName.endsWith('CobraLang.cs') or _fileName.endsWith('SystemInterfaces.cobra') or _fileName.endsWith('Cobra.Lang.dll')

	def addMinFields
		base.addMinFields
		.addField('fileName', _fileName)

	def bindUse
		pass


class AssemblyModule
	is partial
	inherits Module
	"""
	An assembly module represents a .dll
	"""
	
	var _topNameSpace as NameSpace
	var _mustBindInhList = List<of INameSpaceMember>()

	def init(ass as Assembly, globalNS as NameSpace)
		.init(ass, 0, globalNS)

	def init(ass as Assembly, verbosity as int, globalNS as NameSpace)
		base.init(ass.location, verbosity, '')
		_topNameSpace = NameSpace(globalNS, '(top namespace for assembly [ass])')

	def addMustBindInh(what as INameSpaceMember)
		require .compiler.curModule is this
		_mustBindInhList.add(what)

	get topNameSpace from var
		"""
		Returns the top namespace for this module.
		This is an implicit namespace that is not unified (its unified namespace is the global namespace).
		"""

	def bindUse
		base.bindUse
		.topNameSpace.bindUse

	def _bindInh
		base._bindInh
		# .topNameSpace.bindInh - It's too expensive to scan all types in a DLL. Do them as needed.
		for item in _mustBindInhList, item.bindInh

	def _bindInt
		base._bindInt
		# .topNameSpace.bindInt - It's too expensive to scan all types in a DLL. Do them as needed.


class CobraModule
	is partial
	inherits Module

	var _topNameSpace as NameSpace
	
	def init(fileName as String, verbosity as int, docString as String, globalNS as NameSpace)
		base.init(fileName, verbosity, docString)
		_topNameSpace = NameSpace(globalNS, '(top namespace for module [fileName])')

	get topNameSpace from var
		"""
		Returns the top namespace for this module.
		This is an implicit namespace that is not unified (its unified namespace is the global namespace).
		"""

	def addSubFields
		base.addSubFields
		.addField('topNameSpace', _topNameSpace)

	def bindUse
		base.bindUse
		.topNameSpace.adoptDefaultNameSpace
		.topNameSpace.bindUse

	def _bindInh
		base._bindInh
		.topNameSpace.bindInh

	def _bindInt
		base._bindInt
		.topNameSpace.bindInt

	def _computeMatchingBaseMembers
		base._computeMatchingBaseMembers
		.topNameSpace.computeMatchingBaseMembers
		
	def _bindImp
		base._bindImp
		assert .didBindInt
		.topNameSpace.bindImp


class NativeModule
	is abstract
	inherits Module
	"""
	The abstract base class for so-called "native" modules such as SharpModule.
	"""

	def init(fileName as String, verbosity as int, docString as String)
		base.init(fileName, verbosity, docString)
