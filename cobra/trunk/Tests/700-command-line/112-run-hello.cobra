class RunNoOp

	shared

		var _output as String?

		def main
			.run('-ert:no')
			.run('-ert:yes')
			.run('-ert:no')
			.run('-ert:no  -include-tests:no')
			.run('-ert:no  -include-tests:no  -dst:yes')
			.run('-ert:no  -include-tests:yes -dst:yes')
			.run('-ert:yes -include-tests:no  -dst:yes')
			.run('-ert:yes -include-tests:yes -dst:yes')
		
		def run(args as String)
			p = .runCobraExe('[args] 110-hello.cobra', true)
			assert p.exitCode == 0
			assert _output.trim == 'Hello'

		def runCobraExe(args as String, verbose as bool) as System.Diagnostics.Process
			.flush
			cobraPath = CobraCore.findCobraExe
			if cobraPath
				p = System.Diagnostics.Process()
				if CobraCore.isRunningOnMono
					p.startInfo.fileName = 'mono'
					p.startInfo.arguments = cobraPath + ' ' + args
				else
					p.startInfo.fileName = cobraPath
					p.startInfo.arguments = args
				_output = CobraCore.runAndCaptureAllOutput(p)
				if verbose
					print
					print 'cobra', args
					print '[p.startInfo.fileName] [p.startInfo.arguments]'
					print '-------'
					.flush
					if _output.length
						print _output stop
					print '-------'
					.flush
				return p
			else
				throw FallThroughException(cobraPath)

		def flush
			CobraCore.printDestination.flush
