"""
Test that the current Cobra compiler can build itself.
"""

use System.Reflection


class TestCompilerBuild

	def main
		cobraPath = CompileTimeInfo.cobraPath
		assert cobraPath.endsWith('Source/cobra.exe') or cobraPath.endsWith('Source\\cobra.exe')
		
		cobraDir = Path.getDirectoryName(cobraPath)
		Environment.currentDirectory = cobraDir to !
		
		.removeFiles

		p = System.Diagnostics.Process()
		p.startInfo.fileName = cobraPath
		args = [
			'-c',
			'-debug',
			'-turbo',
			'-t:lib',
			'-ert:yes',
			'-files:files-to-compile.text',
			'-namespace:Cobra.Lang.Compiler',
			'-out:Test.Cobra.Lang.Compiler.dll',
			'cobra.cobra',
		]
		p.startInfo.arguments = args.join(' ')
		output = CobraCore.runAndCaptureAllOutput(p).trim
		assert output == 'Compilation succeeded'
		assert File.exists('Test.Cobra.Lang.Compiler.dll')

		# Note that .NET/Windows requires a full path to the DLL
		# whereas Mono will load from the current directory
		fullPath = Path.combine(Environment.currentDirectory, 'Test.Cobra.Lang.Compiler.dll')
		assert File.exists(fullPath)
		assembly = Assembly.loadFrom(fullPath)

		assert assembly
		assert assembly.getName.name == 'Test.Cobra.Lang.Compiler'
		cmdLineClass = assembly.getType('Cobra.Lang.Compiler.CommandLine') to ?
		assert cmdLineClass
		cl = cmdLineClass()
		assert cl.versionString.length
		assert cl.versionString.startsWith('svn:')		

		.removeFiles

		
	def removeFiles
		for fileName in 'Test.Cobra.Lang.Compiler.dll Test.Cobra.Lang.Compiler.dll.pdb Test.Cobra.Lang.Compiler.dll.mdb'.split
			try
				File.delete(fileName)
			catch UnauthorizedAccessException
				# .NET/Windows locks the .dll when it is loaded
				pass

