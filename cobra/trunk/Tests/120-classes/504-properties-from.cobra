class Person

	var _age as int
	
	pro ageProp as int
		get
			return _age
		set
			_age = value
	
	# use 'from' as a shorthand form for the above:
	pro age from var  # var is the preferred form when the variable name is the same (but with an underscore)
	pro age1 from _age

	# and use 'get' and 'set' in place of 'pro' when appropriate:
	set age2 from _age
	get age3 from _age

	# can still use a doc string:
	get age4 from _age
		"""
		An example.
		"""

	def main is shared
		p as Person = Person()
		p.ageProp = 5
		assert p.age==5
		p.age1 = 6
		assert p.age1==6
		p.age2 = 7
		assert p.age3==7
		p.age = 8
		assert p.age==8


class A

	get foo as int
		return 1


class B
	inherits A

	var _foo as int
	
	get foo from var is override


class C

	get foo as int
		return 1

class D
	inherits C

	var _foo as int
	
	get foo from var
		is override
