namespace Test

	class Test

		def main
			is shared

			x as int = 0

			try
				throw Exception('')
				x = 1
			catch
				pass
			finally
				x = 2
			assert x==2

			try
				x = 1
				throw Exception('')
				x = 2
			catch
				pass
			assert x==1


			try
				x = 0
				throw Exception('')
			catch e as Exception
				x = 1
			assert x==1


			try
				x = 0
				throw Exception('')
			catch nre as NullReferenceException
				CobraCore.noOp(nre)
				x = 1
			catch
				x = 2
			assert x==2

			try
				x = 0
				throw Exception('')
			catch Exception  # no variable
				x = 1
			assert x==1

			loc as int
			count as int

			# rethrow an exception
			try
				loc = 1
				try
					loc = 2
					throw Exception('aoeu')
				catch
					loc = 3
					throw
			catch e as Exception
				loc = 4
				assert e.message=='aoeu'
			assert loc==4

			# test that the "success" block executes when no exception is throwd
			try
				loc = 1
				count = 1
			catch
				loc = 2
				count += 1
			success
				loc = 3
				count += 1
			assert loc==3
			assert count==2


			# test the the "catch" block does not execute when no exception is throwd
			try
				loc = 1
				count = 1
			catch
				loc = 2
				count += 1
			success
				loc = 3
				count += 1
			finally
				loc = 4
				count += 1
			assert loc==4
			assert count==3


			# test that the "success" block does not execute when an exception is throwd
			try
				loc = 1
				count = 1
				throw Exception('')
			catch
				loc = 2
				count += 1
			success
				loc = 3
				count += 1
			assert loc==2
			assert count==2


			# just for the heck of it
			try
				pass
			catch
				pass
			success
				pass
			finally
				pass

			# this actually failed code gen at one point
			try
				pass
			success
				pass
			finally
				pass
