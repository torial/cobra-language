# test -compile-if-needed compilation (single file)

class Test

	def main
		file = '110-hello'
		src  = '[file].cobra'
		exe  = '[file].exe'
		
		.rm(exe)
		assert not File.exists(exe)
		cobra = CobraCommand.find
		
		# No exe - compile
		r = cobra.run('-c -cin -v [src]')
		assert 'Checking timestamps' in r.output
		assert '"[exe]" does not exist' in r.output
		assert 'Compiling to produce' in r.output
		assert r.exitCode == 0 and File.exists(exe)
		#print '.' stop
		
		# src younger than exe - no compile
		r = cobra.run('-c -cin -v [src]')
		assert 'Checking timestamps' in r.output
		assert 'No source file is newer' in r.output
		assert r.exitCode == 0 and File.exists(exe)
		#print '.' stop
		
		.touch(src)
		# src older than exe - compile
		r = cobra.run('-c -cin -v [src]')
		assert 'Checking timestamps' in r.output
		assert '"[src]" is newer than "[exe]"' in r.output
		assert 'Compiling to produce' in r.output
		assert r.exitCode == 0 and File.exists(exe)
		#print '.' stop
		
		r = cobra.run('-c -v [src]')
		assert 'Checking timestamps' not in r.output
		assert 'Compiling to produce' in r.output
		assert r.exitCode == 0 and File.exists(exe)
		#print '.' stop
	
		#print
		
	def rm(fileName as String)
		try 
			File.delete(fileName)
		catch
			pass
	
	def touch(fileName as String)
		File.setLastWriteTime(fileName, DateTime.now)	
